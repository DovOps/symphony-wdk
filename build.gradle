plugins {
    id 'java'
    id 'io.freefair.lombok' version '6.0.0-m2'
    id 'jacoco'
    id 'org.springframework.boot' version '2.5.2'

    // code quality
    id 'checkstyle'
    id "com.github.spotbugs" version '4.7.1'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    implementation platform('org.finos.symphony.bdk:symphony-bdk-bom:2.1.8')

    implementation 'org.finos.symphony.bdk:symphony-bdk-core-spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    runtimeOnly 'com.h2database:h2'

    implementation 'org.slf4j:slf4j-api'
    runtimeOnly 'ch.qos.logback:logback-classic'

    implementation platform('org.camunda.bpm:camunda-bom:7.15.0')
    implementation 'org.camunda.bpm:camunda-engine-spring'

    implementation 'org.codehaus.groovy:groovy-all:3.0.8'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.3'
    implementation 'com.github.java-json-tools:json-schema-validator:2.2.14'

    implementation 'org.apache.commons:commons-lang3:3.11'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

test {
    useJUnitPlatform()
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
            element = 'CLASS'
        }
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

bootBuildImage {
    imageName = "workflow-bot"
    builder = "gcr.io/buildpacks/builder"
}

checkstyle {
    configDirectory = rootProject.projectDir
}

// Enable HTML report for spotbugs
tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}
